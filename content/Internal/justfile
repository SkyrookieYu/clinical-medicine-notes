CARD_DIR := "flashcards"

clean:
	fd -I '\.csv$' -x rm

points2csv:
	just convert_csv points

convert_all:
	@for f in `ls {{CARD_DIR}}/*.yaml`; do\
		_fn=`echo $f | cut -d . -f 1`;\
		echo "Converting ${_fn}.yaml to ${_fn}.csv";\
		just convert_csv ${_fn};\
	done

# Provided FILENAME (without extension), convert the yaml file into anki-compatible csv file
convert_csv FILENAME:
	yq -r ".[] | [.UUID, .Q, .A, .created_time] | @csv" "{{FILENAME}}.yaml" > "{{FILENAME}}.csv"
	just _markdown_to_html "{{FILENAME}}"

_markdown_to_html FILENAME:
	#!/usr/bin/env python
	import markdown
	import pandas as pd
	from functools import partial
	import re

	INLINE_MATH_RE = re.compile(r"(?<!\\)\$([^\$]+)\$")
	BLOCK_MATH_RE = re.compile(r"(?<!\\)\$\$([^\$]+)\$\$")
	markdownfn = partial(
		markdown.markdown,
		extensions=['tables', 'fenced_code'],
	)

	def mapfn(text):
		text = markdownfn(text)
		text = BLOCK_MATH_RE.sub(r"\\[\1\\]", text)
		text = INLINE_MATH_RE.sub(r"\\(\1\\)", text)
		return text

	df = pd.read_csv("./{{FILENAME}}.csv", names=["UUID", "Q", "A", "created_time"])
	df.Q = df.Q.map(mapfn)
	df.A = df.A.map(mapfn)
	df.to_csv("./{{FILENAME}}.csv", header=False, index=False)
